managed;
strict ( 2 );

define behavior for ZRAP_I_TRAVEL_M1 alias Travel
implementation in class zbp_rap_i_travel_m1 unique
persistent table /dmo/travel_m
lock master
authorization master ( instance )
etag master LastChangedAt
early numbering
{

determination setStatusToOpen on modify { create; }

  field ( readonly ) TravelId, CreatedAt,CreatedBy,LastChangedAt,LastChangedBy;
  field (mandatory) AgencyID, CustomerID ;
//  field ( readonly ) OverallStatus;
  create;
  update;
  delete;


determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
//determination calculateTravelID   on save   { create; }

// instance action and dynamic action control
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

    factory action copyTravel [1];

    internal action recalcTotalPrice;


//  association Booking { create; }

// dynamic feature control when status is rejected create to be disabled
  association Booking { create ( features : instance ); }
  mapping for /dmo/travel_m
    {
      TravelId      = travel_id;
      AgencyID      = agency_id;
      BeginDate     = begin_date;
      BookingFee    = booking_fee;
      CreatedAt     = created_at;
      CreatedBy     = created_by;
      CurrencyCode  = currency_code;
      CustomerID    = customer_id;
      Description   = description;
      EndDate       = end_date;
      LastChangedAt = last_changed_at;
      LastChangedBy = last_changed_by;
      OverallStatus = overall_status;
      TotalPrice    = total_price;
    }
}

define behavior for ZRAP_I_BOOKING_M1 alias Booking
implementation in class zbp_rap_i_booking_m1 unique
persistent table /dmo/booking_m
lock dependent by Travel
authorization dependent by Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field (mandatory)  CarrierId, ConnectionId;
  association Travel;
  association Booksupplement { create; }

  mapping for /dmo/booking_m
    {
      TravelId      = travel_id;
      BookingDate   = booking_date;
      BookingId     = booking_id;
      BookingStatus = booking_status;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      CurrencyCode  = currency_code;
      CustomerId    = customer_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      LastChangedAt = last_changed_at;
    }

}

define behavior for ZRAP_I_BOOKSUPPL_M1 alias Booksuppl
implementation in class zbp_rap_i_Booksuppl_m1 unique
persistent table /dmo/booksuppl_m
lock dependent by Travel
authorization dependent by Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId,LastChangedAt;
  field (mandatory) Price, CurrencyCode;
  association Travel;
  association Booking;

  mapping for /dmo/booksuppl_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
      Price               = price;
      SupplementId        = supplement_id;
    }
}